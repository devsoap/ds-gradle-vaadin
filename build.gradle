/*
 * Copyright 2018-2019 Devsoap Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.apache.tools.ant.filters.ReplaceTokens

/***********************************************************************************************************************
 *
 * Plugins
 *
 **********************************************************************************************************************/
plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'idea'
    id 'codenarc'
    id 'checkstyle'
    id 'com.gradle.plugin-publish' version '0.9.10'
}

/***********************************************************************************************************************
 *
 * Basic properties
 *
 **********************************************************************************************************************/

defaultTasks 'jar'
group 'com.devsoap'
version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : '2.0-SNAPSHOT-'+ new Date().format('yyyyMMdd')
archivesBaseName = 'gradle-vaadin-flow-plugin'
sourceCompatibility = 1.8
targetCompatibility = 1.8

/***********************************************************************************************************************
 *
 * Sources
 *
 **********************************************************************************************************************/

sourceSets {
    functionalTest {
        groovy {
            srcDir file('src/functionalTest/groovy')
        }
        resources {
            srcDir file('src/functionalTest/resources')
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs){
        filesMatching('versions.properties'){
            filter(ReplaceTokens, tokens: [
                version: project.version,
                signature: System.getenv('PRODUCT_SIGNATURE') ?: 'e1058e32-c659-4606-876d-6ace3703292f'
            ])
        }
    }
}

/***********************************************************************************************************************
 *
 * Repositories & Dependencies
 *
 **********************************************************************************************************************/

repositories {
    mavenCentral()
    jcenter()
    maven { url = 'https://plugins.gradle.org/m2/' }
}

dependencies {
    compile localGroovy()

    compile group: 'com.moowork.node', name: 'com.moowork.node.gradle.plugin', version: '1.3.1'
    compile 'io.github.classgraph:classgraph:4.8.26'
    compile 'com.auth0:java-jwt:3.8.2'

    testCompile('org.spockframework:spock-core:1.3-groovy-2.5')

}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

/***********************************************************************************************************************
 *
 * Artifacts
 *
 **********************************************************************************************************************/

jar {
    manifest{
        attributes(
                'Vaadin-Package-Version': 1,
                'Implementation-Title': 'DS Vaadin Flow Gradle Plugin',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Devsoap Inc.',
        )
    }
    from 'gradle.properties'
}

/**
 * Plugin artifact sources.
 */
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    include 'com/**/*.groovy'
    include 'com/**/*.java'
}

/**
 * Artifacts in build
 */
artifacts {
    archives jar
    archives sourcesJar
}

/**
 * API documentation
 */
groovydoc {
    docTitle = "DS Vaadin Flow Gradle Plugin $version API"
    windowTitle = "DS Vaadin Flow Gradle Plugin $version"
    footer = "DS Vaadin Flow Gradle Plugin is maintained by <a href=\"https://github.com/johndevs/\">@johndevs</a>. Copyright Â© 2018"
}


/***********************************************************************************************************************
 *
 * Testing & Quality
 *
 ***********************************************************************************************************************/
task functionalTest(type: Test) {
    dependsOn test, jar
    group = 'Verification'

    systemProperties = System.properties as Map
    if (!systemProperties['spock.configuration']) {
        systemProperty 'spock.configuration', "${findProperty('TEST_CATEGORY') ?: 'AllTests' }Config.groovy"
    }

    if (!systemProperties['yarn.cache.dir']) {
        systemProperty 'yarn.cache.dir', new File(project.rootProject.projectDir, 'yarn-cache').canonicalPath
    }

    if (!systemProperties['plugin.jar.path']) {
        systemProperty 'plugin.jar.path', jar.archivePath.canonicalPath
    }

    if (!systemProperties['devsoap.gradle-flow-plugin.license.key'] && System.getenv('DEVSOAP_KEY')) {
        systemProperty 'devsoap.gradle-flow-plugin.license.key', System.getenv('DEVSOAP_KEY')
    }

    if (!systemProperties['devsoap.gradle-flow-plugin.license.email'] && System.getenv('DEVSOAP_EMAIL')) {
        systemProperty 'devsoap.gradle-flow-plugin.license.email', System.getenv('DEVSOAP_EMAIL')
    }

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    maxParallelForks = 1
    jvmArgs('-Xms512M', '-Xmx1024M')
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
check.dependsOn functionalTest

codenarc{
    toolVersion = '1.1'
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = 0
    codenarcFunctionalTest {
        configFile = rootProject.file('config/codenarc/ruleset-test.groovy')
    }
    codenarcTest {
        configFile = rootProject.file('config/codenarc/ruleset-test.groovy')
    }
}

checkstyle {
    configDir = rootProject.file('config/checkstyle')
}

plugins.withType(GroovyBasePlugin) {
    sourceSets.all { sourceSet ->
        task "${sourceSet.getTaskName('checkstyle', 'groovy')}"(type: Checkstyle) {
            configFile = rootProject.file('config/checkstyle/checkstyle.xml')
            source sourceSet.allGroovy
            classpath = sourceSet.compileClasspath
            reports.xml.destination rootProject.file("build/reports/checkstyle/${sourceSet.name}.xml")
        }
    }
}

pluginUnderTestMetadata {
    pluginClasspath.from(files(jar.archivePath))

}

/***********************************************************************************************************************
 *
 * Deployment
 *
 ***********************************************************************************************************************/
gradlePlugin {
    testSourceSets sourceSets.functionalTest
    plugins {
        vaadinPlugin {
            id = 'com.devsoap.vaadin-flow'
            implementationClass = 'com.devsoap.vaadinflow.VaadinFlowPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/devsoap/gradle-vaadin-flow/wiki'
    vcsUrl = 'https://github.com/devsoap/gradle-vaadin-flow'
    description = 'Build Vaadin Flow applications with Gradle!'
    tags = ['vaadin', 'vaadin-flow', 'vaadin-elements', 'polymer', 'java', 'groovy', 'kotlin']
    plugins {
        vaadinPlugin {
            id = gradlePlugin.plugins.vaadinPlugin.id
            displayName = 'DS Vaadin Flow Gradle Plugin'
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.archivesBaseName
    }
}

/***********************************************************************************************************************
 *
 * Misc.
 *
 **********************************************************************************************************************/
wrapper {
    gradleVersion = '5.0'
    distributionUrl = distributionUrl.replace('bin', 'all') // Download sources
}

